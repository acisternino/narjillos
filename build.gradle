// Plugins

plugins {
    id 'application'

    // Also applies the following plugins
    // - com.google.osdetector
    // - org.javamodularity.moduleplugin
    // TODO: switch to a stable version once we get both the Apple M1 support,
    // and a fix for the command arguments. For now, we have to explicity add
    // a dependency from an updated, working version of moduleplugin:
    id 'org.openjfx.javafxplugin' version '0.0.12-SNAPSHOT'    // needed for M1 Mac support
    id 'org.javamodularity.moduleplugin' version '1.8.10'      // needed to fix bug in older version that the JFX plugin depends on
}

// Basic Configuration

version = '0.8.1'

group = 'org.nusco.narjillos'

sourceCompatibility = JavaVersion.VERSION_17

ext {
    junitVersion = '5.8.2'
}

application {
    mainModule = 'org.nusco.narjillos'
    mainClass = 'org.nusco.narjillos.application.NarjillosRunner'
}

javafx {
    version = '18-ea+10'  // TODO: update to a stable version once the bugs with Apple's M1 are fixed
    modules = [ 'javafx.graphics' ]
}

// Dependencies

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'commons-cli:commons-cli:1.5.0'
    implementation 'org.yaml:snakeyaml:1.30'
    implementation 'org.xerial:sqlite-jdbc:3.36.0.2'

    // Test
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    // testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"     // optional
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'org.mockito:mockito-core:4.2.0'

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

// Tests

test {
    description "Runs standard suite of unit tests."
    useJUnitPlatform()
    maxHeapSize = "2g"
    filter {
        excludeTestsMatching '*PerformanceTest'
        excludeTestsMatching '*DeterministicExperimentTest'
    }
    testLogging {
        showStandardStreams = true
        events 'FAILED', 'SKIPPED'
    }
}

task(testPerformance, dependsOn: 'compileJava', type: JavaExec) {
    description = 'Runs performance test.'
    group = 'verification'
    mainClass = 'org.nusco.narjillos.application.PerformanceTest'
    classpath = sourceSets.test.runtimeClasspath
}

task(testDeterministic, dependsOn: 'compileJava', type: JavaExec) {
    description = 'Runs the (slow) test that checks whether the system is deterministic.'
    group = 'verification'
    mainClass = 'org.nusco.narjillos.application.DeterministicExperimentTest'
    classpath = sourceSets.test.runtimeClasspath
}

task testAll(dependsOn: ['test', 'testDeterministic', 'testPerformance']) {
    description = 'Runs all the tests, including the slow tests, the database tests and the performance tests.'
    group = 'verification'
}


// Backlog Management

task(bl, dependsOn: 'build', type: JavaExec) {
    mainClass = 'org.nusco.narjillos.application.Backlog'
    classpath = sourceSets.main.runtimeClasspath
    args commandLineArgsOr('all')
}

task(backlog, dependsOn: 'bl') {
    description = 'Prints the top of the backlog. Also aliased to \'bl\'.'
    group = 'development'
}

// Helpers

def commandLineArgsOr(defaultArgs) {
    if (project.hasProperty('args') && project.getProperty('args').trim().length() > 0)
        return project.args.split('\\s+')
    else
        return defaultArgs
}


/*

// Programs

createProgramTask('narjillos', 'org.nusco.narjillos.application.NarjillosRunner', 'Runs Narjillos (same arguments as the \'narjillos\' script).')
createProgramTask('dnabrowser', 'org.nusco.narjillos.application.DNABrowserRunner', 'Runs the DNA Browser (pass it the *.germline filename).')
createProgramTask('lab', 'org.nusco.narjillos.Lab', 'Runs lab analysis (pass it the *.exp filename).')

// Packaging

// TODO: remove these once the issue with duplicatesStrategy has been fixed
// (see: https://github.com/gradle/gradle/issues/17236)
tasks.named('distTar') { duplicatesStrategy = 'include' }
tasks.named('distZip') { duplicatesStrategy = 'include' }
tasks.named('installDist') { duplicatesStrategy = 'include' }
// END TODO

applicationDistribution.from(
  files(
    'version',
    'LICENSE',
    'README.md',
    'config.yaml'
  )
)

task createStartupScripts(dependsOn: 'installDist') {
    createScript('lab', 'org.nusco.narjillos.Lab')
    createScript('dnabrowser', 'org.nusco.narjillos.application.DNABrowserRunner')
}

task release(dependsOn: ['testAll', 'createStartupScripts', 'assemble']) {
    description = 'Runs all tests and packages a release.'
    group = 'distribution'
}

// Helpers

def createProgramTask(taskName, mainClassName, taskDescription) {
    tasks.create(name: taskName, type: JavaExec) {
        description = taskDescription
        group = 'programs'
        mainClass = mainClassName
        classpath = sourceSets.main.runtimeClasspath
        args commandLineArgsOr([])
    }
}

def createScript(name, mainClassName) {
    def taskName = name + 'StartScript'
    tasks.create(name: taskName, type: CreateStartScripts) {
        outputDir = new File(buildDir, 'scripts')
        mainClass = mainClassName
        applicationName = name
        classpath = tasks[JavaPlugin.JAR_TASK_NAME].outputs.files + configurations.runtimeClasspath
    }

    tasks[taskName].dependsOn(project.jar)

    applicationDistribution.with {
        into('bin/') {
            from(tasks[taskName])
            fileMode = 0755
        }
    }
}

*/
